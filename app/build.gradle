apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-parcelize'
apply plugin: 'kotlin-kapt'
apply plugin: 'androidx.navigation.safeargs.kotlin'
apply plugin: 'com.github.triplet.play'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'

import com.github.triplet.gradle.androidpublisher.ResolutionStrategy
import com.github.triplet.gradle.androidpublisher.ReleaseStatus

android {
    compileSdkVersion 29

    defaultConfig {
        applicationId "de.crysxd.octoapp"
        minSdkVersion 21
        targetSdkVersion project.rootProject.ext.target_sdk
        versionCode 1810
        versionName "1.8-beta12"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            Properties properties = new Properties()
            properties.load(project.rootProject.file('local.properties').newDataInputStream())

            storeFile file("key.keystore")
            storePassword properties.getProperty('signing.storePassword')
            keyAlias properties.getProperty('signing.storeAlias')
            keyPassword properties.getProperty('signing.keyPassword')
        }
    }

    buildTypes {
        debug {
            manifestPlaceholders = [firebaseRemoteServicesEnabled:"false"]
        }
        release {
            minifyEnabled false
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            manifestPlaceholders = [firebaseRemoteServicesEnabled:"true"]
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }
    buildFeatures {
        viewBinding true
    }
}


play {
    serviceAccountCredentials.set(file("service-account.json"))
    track.set("internal")
    defaultToAppBundles.set(true)
    resolutionStrategy.set(ResolutionStrategy.IGNORE)
    releaseStatus.set(ReleaseStatus.COMPLETED)
    updatePriority.set(2)
    userFraction.set(1d)
}


dependencies {
    implementation project(':base')
    implementation project(':signin')
    implementation project(':connect-printer')
    implementation project(':print-controls')
    implementation project(':pre-print-controls')

    /*
     * Google Play
     */
    implementation 'com.google.android.play:core-ktx:1.8.1'
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    /*
     * Test
     */
    testImplementation 'junit:junit:4.13'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'

    /*
     * Dagger
     */
    kapt "com.google.dagger:dagger-compiler:$dagger_version"

}

