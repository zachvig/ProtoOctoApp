apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-parcelize'
apply plugin: 'androidx.navigation.safeargs.kotlin'

android {
    compileSdkVersion project.rootProject.ext.target_sdk

    defaultConfig {
        minSdkVersion project.rootProject.ext.min_sdk
        targetSdkVersion project.rootProject.ext.target_sdk

        testInstrumentationRunnerArguments clearPackageData: 'true'
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    testOptions {
        animationsDisabled = true
        execution 'ANDROIDX_TEST_ORCHESTRATOR'
    }

    signingConfigs {
        release {
            Properties properties = new Properties()
            properties.load(project.rootProject.file('local.properties').newDataInputStream())

            storeFile file("key.keystore")
            storePassword properties.getProperty('signing.storePassword')
            keyAlias properties.getProperty('signing.storeAlias')
            keyPassword properties.getProperty('signing.keyPassword')
        }
    }

    defaultConfig {
        if (project.hasProperty("testEnvDoamin")) {
            buildConfigField "String", "TEST_ENV_DOMAIN", "\"${project.property("testEnvDoamin")}\""
        } else {
            buildConfigField "String", "TEST_ENV_DOMAIN", "\"192.168.1.31\""
        }

        if (project.hasProperty("failedTScreenshotDir")) {
            buildConfigField "String", "FAILED_TEST_SCREENSHOT_DIR", "\"${project.property("failedTScreenshotDir")}\""
        } else {
            buildConfigField "String", "FAILED_TEST_SCREENSHOT_DIR", "\"/storage/emulated/0/Download/test-failures/\""
        }
    }

    buildTypes {
        debug {
            manifestPlaceholders = [firebaseRemoteServicesEnabled: "true"]
        }
        release {
            minifyEnabled false
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            manifestPlaceholders = [firebaseRemoteServicesEnabled: "true"]
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }
    buildFeatures {
        viewBinding true
    }
}

dependencies {
    /*
     * Dagger
     */
    implementation "com.google.dagger:dagger:$dagger_version"
    kapt "com.google.dagger:dagger-compiler:$dagger_version"

    /*
     * UI
     */
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.1'
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.4.0"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.4.0"
    implementation "androidx.core:core-ktx:1.7.0"
    implementation 'com.google.android.flexbox:flexbox:3.0.0'
    implementation 'com.google.android.material:material:1.4.0'
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.1.0"

    /*
     * Firebase
     */
    implementation platform('com.google.firebase:firebase-bom:29.0.0')
    implementation 'com.google.firebase:firebase-auth-ktx'
    implementation 'com.google.firebase:firebase-analytics-ktx'
    implementation 'com.google.firebase:firebase-config-ktx'
    implementation 'com.google.firebase:firebase-crashlytics'
    implementation 'com.google.firebase:firebase-messaging-ktx'

    /*
    * Markdown
    */
    final def markwon_version = '4.6.2'
    implementation "io.noties.markwon:core:$markwon_version"
    implementation "io.noties.markwon:image:$markwon_version"
    implementation "io.noties.markwon:linkify:$markwon_version"
    implementation 'pl.droidsonroids.gif:android-gif-drawable:1.2.22'

    /*
     * More transitions <3
     */
    implementation 'com.andkulikov:transitionseverywhere:2.1.0'

    /*
     * Logging
     */
    implementation 'com.jakewharton.timber:timber:4.7.1'

    /*
     * Octoprint
     */
    implementation project(':octoprint')

    /*
     * Preferences
     */
    implementation "androidx.preference:preference-ktx:1.1.1"
    implementation 'com.google.code.gson:gson:2.8.7'

    /*
     * Pictures
     */
    implementation 'com.squareup.picasso:picasso:2.71828'

    /*
    * Test
    */
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'com.google.truth:truth:1.0.1'
    testImplementation 'com.nhaarman.mockitokotlin2:mockito-kotlin:2.2.0'
    testImplementation 'com.google.truth:truth:1.0.1'

    androidTestImplementation 'com.nhaarman.mockitokotlin2:mockito-kotlin:2.2.0'
    androidTestImplementation 'com.linkedin.dexmaker:dexmaker-mockito-inline:2.28.1'
    androidTestImplementation 'androidx.test:runner:1.4.0'
    androidTestImplementation 'androidx.test:rules:1.4.0'
    androidTestImplementation 'com.google.truth:truth:1.0.1'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation 'com.google.truth:truth:1.0.1'
    androidTestUtil 'androidx.test:orchestrator:1.4.0'
    androidTestImplementation('com.adevinta.android:barista:4.1.0') {
        exclude group: 'org.jetbrains.kotlin' // Only if you already use Kotlin in your project
    }
}